programa pr5ej3
procesos
  proceso juntarFlores(ES cantFlores: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cantFlores:= cantFlores + 1
  fin
  proceso irAlDeposito(ES cantFlores: numero; E tipoAccion: numero)
  variables
    oldAv, oldCa: numero
  comenzar
    oldAv:= PosAv
    oldCa:= PosCa
    BloquearEsquina(10, 10)
    Pos(10, 10)
    si (tipoAccion = 0)
      repetir 10
        depositarFlor
      cantFlores:= cantFlores - 10
    sino {Cosechador}
      mientras (HayFlorEnLaEsquina)
        tomarFlor
        cantFlores:= cantFlores + 1
    Pos(oldAv, oldCa)
    LiberarEsquina(10, 10)
  fin
areas
  calleR1: AreaP(1, 3, 100, 3)
  calleR2: AreaP(1, 4, 100, 4)
  esqComun: AreaC(10, 10, 10, 10)
  esqCosechador: AreaP(1, 5, 1, 5)
robots
  robot tipoRecolector
  variables
    cantFlores, miId: numero
  comenzar
    {Inicializar variables}
    cantFlores:= 0
    derecha
    RecibirMensaje(miId, cosechador)
    repetir 99
      juntarFlores(cantFlores)
      si (cantFlores > 9)
        irAlDeposito(cantFlores, 0)
        EnviarMensaje(V, cosechador)
      mover
    {Avisarle al cosechador que terminaste el trabajo}
    EnviarMensaje(F, cosechador)
  fin
  robot tipoCosechador
  variables
    cantFlores: numero
    confirmacion: boolean
  comenzar
    {Inicializar variables}
    cantFlores:= 0
    {Enviar identificadores}
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    {Cosechar flores}
    repetir 2 {Por cada robot}
      RecibirMensaje(confirmacion, *)
      mientras (confirmacion)
        irAlDeposito(cantFlores, 1)
        RecibirMensaje(confirmacion, *)
    Informar("cantidad", cantFlores)
  fin
variables
  cosechador: tipoCosechador
  r1: tipoRecolector
  r2: tipoRecolector
comenzar
  AsignarArea(r1, calleR1)
  AsignarArea(r2, calleR2)
  AsignarArea(cosechador, esqCosechador)
  {Area que comparten los robots}
  AsignarArea(r1, esqComun)
  AsignarArea(r2, esqComun)
  AsignarArea(cosechador, esqComun)
  {Inicializar robots}
  Iniciar(r1, 1, 3)
  Iniciar(r2, 1, 4)
  Iniciar(cosechador, 1, 5)
fin
